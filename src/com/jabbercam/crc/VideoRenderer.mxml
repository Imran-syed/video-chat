<?xml version="1.0" encoding="utf-8"?>
<!--
/**
 * VIDEOSOFTWARE.PRO
 * Copyright 2010 VideoSoftware.PRO
 * All Rights Reserved.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License
 *  as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 *  warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 *  See the GNU General Public License for more details.
 *  You should have received a copy of the GNU General Public License along with this program.
 *  If not, see <http://www.gnu.org/licenses/>.
 * 
 *  Author: our fast growing online community at videosoftware.pro
 */
-->
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="300">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			protected var _video : Video;
			
			protected var _netStream : NetStream;
			
			protected var _camera : Camera;
			
			[Bindable]
			public function set netStream(value : NetStream) : void {
				_netStream = value;
				
				if(_video) {
					_video.clear();
					_video.attachNetStream(_netStream);
				}
			}
			
			public function get netStream() : NetStream {
				return _netStream;
			}
			
			[Bindable]
			public function set camera(value : Camera) : void {
				_camera = value;
				
				if(_video) {
					_video.attachCamera(_camera);
				}
			}
			
			public function get camera() : Camera {
				return _camera;
			}

			protected function videoContainer_resizeHandler(event:ResizeEvent):void
			{
				if(_video) {
					_video.width = videoContainer.width;
					_video.height = videoContainer.height;
				}
			}


			protected function videoContainer_creationCompleteHandler(event:FlexEvent):void
			{
				_video = new Video(videoContainer.width, videoContainer.height);
				_video.smoothing = true;
				videoContainer.addChild(_video);
				
				if(_netStream)
					_video.attachNetStream(_netStream);
				else if(_camera)
					_video.attachCamera(_camera);
			}

			public function stopVideo() : void {
				if(_video) {
					_video.attachCamera(null);
					_video.attachNetStream(null);
					_camera = null;
					_netStream = null;
					_video.clear();
				}
			}
		]]>
	</fx:Script>
	<mx:UIComponent id="videoContainer" width="100%" height="100%" resize="videoContainer_resizeHandler(event)"
					creationComplete="videoContainer_creationCompleteHandler(event)"/>
</mx:Canvas>
